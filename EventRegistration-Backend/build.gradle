plugins {
	id 'java'
	id 'org.springframework.boot' version '3.0.2'
	id 'io.spring.dependency-management' version '1.1.0'
	id 'jacoco'
}

group = 'ca.mcgill.ecse321'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	runtimeOnly 'org.postgresql:postgresql'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// API docs
	// See springdoc.org/v2/#getting-started and springdoc.org/v2/#javadoc-support
	implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.0.3'
	implementation 'org.springdoc:springdoc-openapi-starter-common:2.0.3'
	annotationProcessor 'com.github.therapi:therapi-runtime-javadoc-scribe:0.13.0'
	implementation 'com.github.therapi:therapi-runtime-javadoc:0.13.0'
	
	// Code coverage
}

tasks.named('test') {
	useJUnitPlatform()
	
	testLogging {
		exceptionFormat "full"
		events "failed", "skipped", "passed"
	}
	
	// After running the tests, generate the coverage report and require sufficient coverage
	finalizedBy jacocoTestReport
	finalizedBy jacocoTestCoverageVerification
}

jacocoTestReport {
	// Run the tests before generating the coverage report
	dependsOn test
}

jacocoTestCoverageVerification {
	// Run the tests before checking coverage
	dependsOn test

	violationRules {
		rule {
			limit {
				minimum = 0.6
			}
		}
	}
}
